apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'org.openapi.generator'

import org.openapitools.generator.gradle.plugin.tasks.GenerateTask

task openApiGenerateAll() {
    fileTree("$rootDir/app/swagger/").matching {
        include "/*.yaml"
    }.each { file ->
        def taskName = "openApiGenerate" + file.getName().capitalize().replace(".yaml", "")
        println("Creating gradle task: " + taskName + "...")
        tasks.create( taskName, GenerateTask.class, {
            generatorName = "kotlin"
            inputSpec = "$rootDir/app/swagger/".toString() + "${file.getName()}"
            outputDir = "$buildDir/generated".toString()
            templateDir = "$rootDir/templates/kotlin-client".toString()
            modelPackage = "com.citymobil.feature.dto"
            globalProperties = [
                    modelDocs: "false",
                    models: ""
            ]
            configOptions = [
                    serializationLibrary: "gson"
            ]
        })

        openApiGenerateAll.dependsOn(taskName)
    }
}

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.valkoz.swaggergenerator"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation"org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support.constraint:constraint-layout:2.0.4'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}
