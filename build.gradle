// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext.kotlin_version = '1.3.41'
    repositories {
        google()
        jcenter()
        maven { url "https://repo1.maven.org/maven2" }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.0.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.openapitools:openapi-generator-gradle-plugin:5.0.0-beta2"
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        google()
        jcenter()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

subprojects {
    task openApiGenerateAll() {
        fileTree("$projectDir").matching {
            include "swagger/*.yaml"
        }.each { file ->
            def taskName = "openApiGenerate" + file.getName().capitalize().replace(".yaml", "")
            def packageName = "com.citymobil." + projectDir.name.replace("-", ".") + ".dto"
            tasks.create( taskName, org.openapitools.generator.gradle.plugin.tasks.GenerateTask.class, {
                generatorName = "kotlin"
                inputSpec = "${file.getAbsolutePath()}"
                outputDir = "$buildDir/generated/source/swagger"
                templateDir = "$rootDir/templates/kotlin-client"
                modelPackage = packageName
                modelNameSuffix = "Dto"
                globalProperties = [
                        modelDocs: "false",
                        models: ""
                ]
                configOptions = [
                        serializationLibrary: "gson"
                ]
            })

            openApiGenerateAll.dependsOn(taskName)
        }
    }
}